<?xml version="1.0" encoding="utf-8"?>
<s:Panel xmlns:fx="http://ns.adobe.com/mxml/2009"
		 xmlns:s="library://ns.adobe.com/flex/spark"
		 xmlns:mx="library://ns.adobe.com/flex/mx"
		 width="400" height="300"
		 backgroundColor="0xcccccc" chromeColor="#CFCFCF" 
		 addedToStage="panel1_addedToStageHandler(event)"
		 dragExit="onDragDropHandler(event)" 
		 resize="panel1_resizeHandler(event)">
	<fx:Script>
		<![CDATA[
			import avmplus.getQualifiedClassName;
			
			import data.Direction;
			
			import event.UIEvent;
			
			import flash.utils.describeType;
			
			import ghostcat.ui.CursorSprite;
			
			import manager.LayerManager;
			
			import mhqy.ui.label.MAssetLabel;
			
			import mx.controls.Alert;
			import mx.controls.Tree;
			import mx.core.UIComponent;
			import mx.events.DragEvent;
			import mx.events.ResizeEvent;
			
			import spark.core.IEditableText;
			
			import uidata.UIClassInfo;
			import uidata.UIElementBaseInfo;
			import uidata.vo.UIClassVo;
			
			import utils.UIElementCreator;
			
			private var _currentClass:String;
			
			//容器
			private var _uiContainerList:Vector.<ClassComponent> = new Vector.<ClassComponent>(10);
			private var _uiContainer:UIComponent;
			private var _hitContainer:UIComponent;//选择热区辅助
			private var _uiMask:UIComponent;
			
			private var _currentClassComponent:ClassComponent;
			
			private var _direction:int = 0;
			
			//拖动区域
			private var _oldX:Number;
			private var _oldY:Number;
			
			public function set currentClass(value:String):void
			{
				App.currentClass = value;
				_currentClass = value;
				_uiContainer.graphics.clear();
				title = "舞台";
				if(value)
				{
					title = "舞台" + "    class:   [" + value + "]";
					
					_uiContainer.graphics.beginFill(0xffffff,1);
					_uiContainer.graphics.drawRect(0,0,3000,3000);
					_uiContainer.graphics.endFill();
				}
			}
			
			protected function panel1_addedToStageHandler(event:Event):void
			{
				App.layerManager.stagePanel = this;
				
				_uiMask = new UIComponent();
				addElement(_uiMask);
				_uiMask.graphics.beginFill(0,0);
				_uiMask.graphics.drawRect(0,0,this.width,this.height);
				_uiMask.graphics.endFill();
					
				_uiContainer = new UIComponent();
				addElement(_uiContainer);
				_uiContainer.mask = _uiMask;
//				_uiContainer.addChild(new CursorSprite());
				_hitContainer = new UIComponent();
				addElement(_hitContainer);
				
				for (var i:int = 0; i < _uiContainerList.length; i++) 
				{
					var con:ClassComponent = new ClassComponent();
					_uiContainer.addChild(con);
					_uiContainerList[i] = con;
				}
				_currentClassComponent = _uiContainerList[0];
				
				initEvent();
			}
			
			private function initEvent():void
			{
				this.addEventListener(MouseEvent.MOUSE_DOWN, onSelectedHotRectHandler);
				this.addEventListener(KeyboardEvent.KEY_DOWN,onKeyDownHandler);
				stage.addEventListener(KeyboardEvent.KEY_UP,onKeyUpHandler);	
				this.addEventListener(MouseEvent.RIGHT_MOUSE_DOWN,onRightDownHandler);
				stage.addEventListener(MouseEvent.RIGHT_MOUSE_UP,onRightUpHandler);
				App.addEventListener(UIEvent.LAYERS_SELECT_UPDATE,onSelectUpdateHandler);
				App.addEventListener(UIEvent.SELECT_A_CLASS,selectAClassHandler,false,99);
				
				//scales
				App.addEventListener(UIEvent.STAGE_ENLARGE,enlargeHandler);
				App.addEventListener(UIEvent.STAGE_LESSON,lessonHandler);
				App.addEventListener(UIEvent.STAGE_NO_SCALE,noScaleHandler);
				
				//alt copy
				App.addEventListener(UIEvent.ALT_COPY_INFOS,altCopyHandler);
				
				//helpClass update
				App.addEventListener(UIEvent.HELP_CLASS_UPDATE,helpClassUpdateHandler,false,99);
				App.addEventListener(UIEvent.HELP_CLASS_SELECTED,helpClassSelectHandler);
			}
			
			protected function onKeyDownHandler(evt:KeyboardEvent):void
			{
				switch(evt.keyCode)
				{
					case Keyboard.A:
					case Keyboard.LEFT:
						_direction = _direction | Direction.LEFT;
						break;
					case Keyboard.D:
					case Keyboard.RIGHT:
						_direction = _direction | Direction.RIGHT;
						break;
					case Keyboard.W:
					case Keyboard.UP:
						if(evt.ctrlKey && evt.shiftKey)
						{
							App.layerManager.layerHandler(LayerManager.TOP);
						}else if(evt.ctrlKey)
						{
							App.layerManager.layerHandler(LayerManager.UP);
						}else{
							_direction = _direction | Direction.UP;
						}
						break;
					case Keyboard.S:
					case Keyboard.DOWN:
						if(evt.ctrlKey && evt.shiftKey)
						{
							App.layerManager.layerHandler(LayerManager.BOTTOM);
						}else if(evt.ctrlKey)
						{
							if(evt.keyCode == Keyboard.S)App.xmlParser.save();
							else if(evt.keyCode == Keyboard.DOWN)App.layerManager.layerHandler(LayerManager.DOWN);
						}else{
							_direction = _direction | Direction.DOWN;
						}
						break;
					case Keyboard.DELETE:
						App.layerManager.layerHandler(LayerManager.DEL);
						break;
					case Keyboard.NUMBER_1:
						if(evt.ctrlKey)App.dispathEvent(new UIEvent(UIEvent.STAGE_NO_SCALE));
						break;
					case Keyboard.EQUAL:
						if(evt.ctrlKey)App.dispathEvent(new UIEvent(UIEvent.STAGE_ENLARGE));
						break;
					case Keyboard.MINUS:
						if(evt.ctrlKey)App.dispathEvent(new UIEvent(UIEvent.STAGE_LESSON));
						break;
					case Keyboard.C:
						if(evt.ctrlKey)copyInfos();
						break;
					case Keyboard.V:
						if(evt.ctrlKey)pasteInfos(App.copyInfos);
						break;
					
				}
				if(_direction != 0)
				{
					this.addEventListener(Event.ENTER_FRAME,onETHandler);
				}
			}
			
			protected function onKeyUpHandler(evt:KeyboardEvent):void
			{
				switch(evt.keyCode)
				{
					case Keyboard.A:
					case Keyboard.LEFT:
						_direction = _direction & 0;
						break;
					case Keyboard.D:
					case Keyboard.RIGHT:
						_direction = _direction & 0;
						break;
					case Keyboard.W:
					case Keyboard.UP:
						_direction = _direction & 0;	
						break;
					case Keyboard.S:
					case Keyboard.DOWN:
						_direction = _direction & 0;	
						break;
				}
				if(_direction == 0)
				{
					this.removeEventListener(Event.ENTER_FRAME,onETHandler);
				}
			}
			
			protected function onETHandler(event:Event):void
			{
				App.hotRectManager.moveDir(_direction);	
			}
			
			private function onSelectedHotRectHandler(evt:MouseEvent):void 
			{
				stage.focus = this;
				if(App.hotRectManager.selectedRects.length > 0)
				{
					App.hotRectManager.unSelectAll();
					App.dispathEvent(new UIEvent(UIEvent.STAGE_SELECT_UPDATE,[]));
				}
				
				_oldX = _uiContainer.mouseX * _uiContainer.scaleX;
				_oldY = _uiContainer.mouseY * _uiContainer.scaleY;
				
				stage.addEventListener(MouseEvent.MOUSE_MOVE,onMouseMoveHandler);
				stage.addEventListener(MouseEvent.MOUSE_UP,onMouseUpHandler);
			}
			
			protected function onDragDropHandler(evt:DragEvent):void
			{
				if(!_currentClass)
				{
					Alert.show("请先选择或新建一个类");
					return;
				}
				
				var value:Object = Object(evt.dragInitiator).value;
				trace("接受=======",value);
				
				//库面板
				if(value is UIClassVo)
				{
					var vlassVO:UIClassVo = value as UIClassVo;
					
					var info:UIElementBaseInfo = vlassVO.info;
					var uiInfo:UIElementBaseInfo = info.clone();
					uiInfo.x = _uiContainer.mouseX;
					uiInfo.y = _uiContainer.mouseY;
					
					App.hotRectManager.unSelectAll();
					addHotRect(_currentClassComponent,uiInfo,true);
					
					App.layerManager.layerPanel.setChild(_currentClassComponent);
					stage.focus = this;
				}//类面板
				else if(value is String)
				{
					if(_currentClass == value)return;
					var classInfo:UIClassInfo = App.classInfoList.getClassInfo(_currentClass);
					if(!classInfo)return;
					classInfo.addHelpClass(value.toString());
				}
			}
			
			private function onSelectUpdateHandler(evt:UIEvent):void
			{
				var arr:Array = evt.data as Array;
				for (var i:int = 0; i < _currentClassComponent.numChildren; i++) 
				{
					var item:HotRectControl = _currentClassComponent.getChildAt(i) as HotRectControl;
					var bool:Boolean = false;
					for (var j:int = 0; j < arr.length; j++) 
					{
						if(i == arr[j])
						{
							bool = true;
							break;
						}
					}
					item.selected = bool;
				}
			}
			
			private var _updateLayerFlag:Boolean = false;
			
			private function updateLate():void
			{
				if(_updateLayerFlag == true)return;
				_updateLayerFlag = true;
				callLater(updateLayerSelects);
			}
			
			private function updateLayerSelects():void
			{
				_updateLayerFlag = false;
				var arr:Array = [];
				for (var i:int = 0; i < _currentClassComponent.numChildren; i++) 
				{
					var item:HotRectControl = _currentClassComponent.getChildAt(i) as HotRectControl;
					if(item.selected)arr.push(i);
				}
				App.dispathEvent(new UIEvent(UIEvent.STAGE_SELECT_UPDATE,arr));
			}
			
			protected function onMouseUpHandler(event:MouseEvent):void
			{
				stage.removeEventListener(MouseEvent.MOUSE_MOVE,onMouseMoveHandler);
				stage.removeEventListener(MouseEvent.MOUSE_UP,onMouseUpHandler);
				
				for (var i:int = 0; i < _currentClassComponent.numChildren; i++) 
				{
					var hotRect:HotRectControl = _currentClassComponent.getChildAt(i) as HotRectControl;
					if(hotRect)
					{
						if(hotRect.hitTestObject(_hitContainer))hotRect.selected = true;
					}
				}
				_hitContainer.graphics.clear();
			}
			
			protected function onMouseMoveHandler(evt:MouseEvent):void
			{
				_hitContainer.graphics.clear();
				if(!evt.altKey)
				{
					_hitContainer.graphics.lineStyle(1,0x00ff00,1);
					_hitContainer.graphics.drawRect(rect.x,rect.y,rect.width,rect.height);
					_hitContainer.graphics.endFill();
				}
			}
			
			private function get rect():Rectangle
			{
				var nx:Number = _uiContainer.mouseX * _uiContainer.scaleX;
				var ny:Number = _uiContainer.mouseY * _uiContainer.scaleY;
				return new Rectangle(
					(_oldX < nx) ? _oldX : nx,
					(_oldY < ny) ? _oldY : ny,
					Math.abs(nx - _oldX),
					Math.abs(ny - _oldY)
					);
			}
			
			protected function onRightDownHandler(event:MouseEvent):void
			{
				_uiContainer.startDrag(false);
				Mouse.cursor = MouseCursor.HAND;
			}
			
			protected function onRightUpHandler(event:MouseEvent):void
			{
				_uiContainer.stopDrag();
				Mouse.cursor = MouseCursor.AUTO;
				_hitContainer.x = _uiContainer.x;
				_hitContainer.y = _uiContainer.y;
			}
			
			protected function panel1_resizeHandler(event:ResizeEvent):void
			{
				if(_uiMask)
				{
					_uiMask.graphics.clear();
					_uiMask.graphics.beginFill(0,0);
					_uiMask.graphics.drawRect(0,0,this.width,this.height);
					_uiMask.graphics.endFill();
				}
			}
			
			private function selectAClassHandler(evt:UIEvent):void
			{
				if(!App.classInfoList.hasClass(String(evt.data)))
				{
					if(String(evt.data) == _currentClass)
					{
						clear();
						currentClass = "";
					}
					return;
				}
				//temp-------------------------
//				if(_currentClass == evt.data.toString())
//				{
//					return;
//				}
				save();
				clear();
				currentClass = evt.data.toString();
				//实例化
				var classInfo:UIClassInfo = App.classInfoList.getClassInfo(_currentClass);
				if(!classInfo)return;
				for (var j:int = 0; j < classInfo.helpClassList.length; j++) 
				{
					var temp:UIClassInfo = App.classInfoList.getClassInfo(classInfo.helpClassList[j].className);
					if(!temp)
					{
						App.log.error("找不到类：",classInfo.helpClassList[j].className);
						continue;
					}
					_uiContainerList[j].className = classInfo.helpClassList[j].className;
					for (var i:int = 0; i < temp.childrenInfo.length; i++) 
					{
						addHotRect(_uiContainerList[j],temp.childrenInfo[i]);
					}
					if(classInfo.helpClassList[j].className == _currentClass)
					{
						_currentClassComponent = _uiContainerList[j];
					}
				}
				
				App.layerManager.layerPanel.setChild(_currentClassComponent);
				stage.focus = this;
			}
						
			private function addHotRect(container:ClassComponent,uiInfo:UIElementBaseInfo,isSelected:Boolean=false,isInDrag:Boolean=false):void
			{
				var hotRect:HotRectControl = new HotRectControl();
				hotRect.selctedHander = updateLate;
				container.addChild(hotRect);
				
				hotRect.uiInfo = uiInfo;
				if(isSelected)hotRect.selected = true;
				if(isInDrag)hotRect.startDrag();
			}
			
			private function clear():void
			{
				for (var i:int = 0; i < _uiContainerList.length; i++) 
				{
					_uiContainerList[i].clear();
				}
			}
			
			public function save():void
			{
				if(!_currentClass)return;
				for (var i:int = 0; i < _uiContainerList.length; i++) 
				{
					var className:String = _uiContainerList[i].className;
					var info:UIClassInfo = App.classInfoList.getClassInfo(className);
					if(!info)continue;
					info.childrenInfo = new Vector.<UIElementBaseInfo>();
					var len:int = _uiContainerList[i].numChildren;
					for (var j:int = 0; j < len; j++) 
					{
						var hotRect:HotRectControl = _uiContainerList[i].getChildAt(j) as HotRectControl;
						if(hotRect && hotRect.uiInfo)
						{
							info.childrenInfo.push(hotRect.uiInfo);
						}
					}
				}
			}
			
			private function enlargeHandler(evt:UIEvent):void
			{
				_uiContainer.scaleX += 0.2;
				_uiContainer.scaleY += 0.2;
			}
			private function lessonHandler(evt:UIEvent):void
			{
				_uiContainer.scaleX -= 0.2;
				_uiContainer.scaleY -= 0.2;
			}
			private function noScaleHandler(evt:UIEvent):void
			{
				_uiContainer.scaleX = _uiContainer.scaleY = 1;
//				_uiContainer.x = _uiContainer.y = 0;
			}
			
			private function copyInfos():void
			{
				App.copyInfos = new Vector.<UIElementBaseInfo>();
				var len:int = _uiContainer.numChildren;
				for (var i:int = 0; i < len; i++) 
				{
					var hotRect:HotRectControl = _uiContainer.getChildAt(i) as HotRectControl;
					if(hotRect && hotRect.uiInfo && hotRect.selected)
					{
						var info:UIElementBaseInfo = hotRect.uiInfo.clone();
						info.x += 10;
						info.y += 10;
						App.copyInfos.push(info);
					}
				}
			}
			
			/**
			 * @param vec
			 * @param isSelected  是否被选中
			 * @param isInDrag  是否在拖动状态[如，alt复制元素为拖动状态]
			 */				
			private function pasteInfos(vec:Vector.<UIElementBaseInfo>,isSelected:Boolean=true,isInDrag:Boolean=false):void
			{
				if(!vec)return;
				if(vec.length >= 1)App.hotRectManager.unSelectAll();
				for (var i:int = 0; i < vec.length; i++) 
				{
					var info:UIElementBaseInfo = vec[i].clone();
					addHotRect(_currentClassComponent,info,isSelected,isInDrag);
				}
				App.layerManager.layerPanel.setChild(_uiContainer);
			}
			
			private function altCopyHandler(evt:UIEvent):void
			{
				var vec:Vector.<UIElementBaseInfo> = evt.data as Vector.<UIElementBaseInfo>;
				pasteInfos(vec,true,true);
			}
			
			private function helpClassUpdateHandler(evt:UIEvent):void
			{
				//temp---------------
//				App.dispathEvent(new UIEvent(UIEvent.SELECT_A_CLASS,App.currentClass));
				evt.data = App.currentClass;
				selectAClassHandler(evt);
			}
			
			private function helpClassSelectHandler(evt:UIEvent):void
			{
				App.hotRectManager.unSelectAll();
				var className:String  = evt.data.toString();
				
				if(_currentClass == className)
				{
					title = "舞台" + "    class:   [" + _currentClass + "]";
				}else
				{
					title = "舞台" + "    class:   [" + _currentClass + "]---类内编辑：" + className;
				}
				
				var info:UIClassInfo = App.classInfoList.getClassInfo(App.currentClass);
				
				for (var i:int = 0; i < _uiContainerList.length; i++) 
				{
					if(className == _uiContainerList[i].className)
					{
						_uiContainerList[i].acitive = true;
						_currentClassComponent = _uiContainerList[i]; 
						App.layerManager.layerPanel.setChild(_currentClassComponent);
					}else
					{
						_uiContainerList[i].acitive = false;
					}
					
					if(i < info.helpClassList.length)_uiContainerList[i].visible = info.helpClassList[i].visible;
					
					if(info.getHelpClassIndex(_uiContainerList[i].className) == -1)
					{
						_uiContainerList[i].clear();
					}
				}
				
				//层级关系
				for (var j:int = 0; j < info.helpClassList.length; j++) 
				{
					for (var k:int = 0; k < _uiContainerList.length; k++) 
					{
						if(_uiContainerList[k].className == info.helpClassList[j].className)
						{
							_uiContainer.setChildIndex(_uiContainerList[k],j);
						}
					}
				}
			}
		]]>
	</fx:Script>
	<fx:Declarations>
		<!-- 将非可视元素（例如服务、值对象）放在此处 -->
	</fx:Declarations>
	<s:Scroller width="100%" height="100%">
		<s:Group> 
		</s:Group>        
	</s:Scroller>
</s:Panel>
