<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx" width="400" height="28">
	<fx:Declarations>
		<!-- 将非可视元素（例如服务、值对象）放在此处 -->
	</fx:Declarations>
	<fx:Script>
		<![CDATA[
			import data.PropertyType;
			
			import event.UIEvent;
			
			import fl.data.DataProvider;
			
			import mx.core.UIComponent;
			import mx.events.ItemClickEvent;
			
			import spark.events.IndexChangeEvent;
			import spark.events.TextOperationEvent;
			
			import uidata.UIElementBaseInfo;
			import uidata.vo.PropertyVo;
			
			private var _info:UIElementBaseInfo;
			private var _property:String;
			private var _propertyVo:PropertyVo;
			
			[Bindable]
			public var toolTips:String="";
			
			private function update():void
			{
				if(_info[_property] == getValue())return;
				_info[_property] = getValue();
//				_info.dispatchEvent(new UIEvent(UIEvent.INFO_UPDATE_PROPERTY,_propertyVo.isChangeView));
				_info.update(_property);
			}
			
			public function setData(property:String,propertyVo:PropertyVo,info:UIElementBaseInfo):void
			{
				if(!property)return;
				toolTips = propertyVo.toolTip;
				
				enabled = propertyVo.isCanEdit;
				
				_property = property;
				_propertyVo = propertyVo;
				_info = info;
				
				valueLabel.visible = false;
				combobox.visible = false;
				colorPicker.visible = false;
				radioGroup.visible = false;
				
				switch(_propertyVo.propertyType)
				{
					case PropertyType.DATAPROVIDER:
						if(_propertyVo.dataProvider.length > 2)
						{
							combobox.visible = true;
							combobox.dataProvider = _propertyVo.dataProvider;
							for(var i:int=0;i<combobox.dataProvider.length;i++){
								var obj:Object = combobox.dataProvider.getItemAt(i);
								if(obj.data == info[property])
								{
									combobox.selectedIndex = i;
									break;
								}
							}
							combobox.rowCount = _propertyVo.dataProvider.length;
						}else if(_propertyVo.dataProvider.length == 2)
						{
							radioGroup.visible = true;
							radio1.label = _propertyVo.dataProvider[0].label;
							radio2.label = _propertyVo.dataProvider[1].label;
							
							radio1.value = _propertyVo.dataProvider[0].data;
							radio2.value = _propertyVo.dataProvider[1].data;
								
							if(info[property] == _propertyVo.dataProvider[0].data)
							{
								radio1.selected = true;	
							}else if(info[property] == _propertyVo.dataProvider[1].data)
							{
								radio2.selected = true;
							}
						}
						break;
					case PropertyType.NUMBER:
						valueLabel.visible = true;
						valueLabel.restrict = "0-9";		
						break;
					case PropertyType.STRING:
						valueLabel.visible = true;
						valueLabel.restrict = "^@";
						break;
					case PropertyType.COLOR:
						valueLabel.width = 151;
						valueLabel.visible = true;
						colorPicker.visible = true;
						valueLabel.restrict = "0xX0-9";
						break;
				}
				
				nameLabel.text = property;
				valueLabel.text = String(info[property]);
			}
			
			protected function valueLabel_focusOutHandler(event:FocusEvent):void
			{
				update();
			}
			
			public function updateView():void
			{
				setData(_property,_propertyVo,_info);
			}
			
			public function dispose():void
			{
				if(parent)
				{
					Group(parent).removeElement(this);
				}
			}
			
			protected function combobox_changeHandler(event:Event):void
			{
				update();				
			}
			
			protected function valueLabel_keyDownHandler(evt:KeyboardEvent):void
			{
				if(evt.keyCode == Keyboard.ENTER || evt.keyCode == Keyboard.TAB)
				{
					update();
					App.dispathEvent(new UIEvent(UIEvent.PRPPERTY_FOCUS_CHANGE,this));
				}
			}
			
			public function setFocusIn():Boolean
			{
				if(!visible)return true;
				if(valueLabel.visible && _propertyVo && _propertyVo.isCanEdit)
				{
					valueLabel.setFocus();
					return true;
				}
				return false;
			}
			
			private function getValue():*
			{
				if(_propertyVo.dataProvider)
				{
					if(radioGroup.visible)
					{
						if(radio1.selected)
						{
							return radio1.value; 
						}
						if(radio2.selected)
						{
							return radio2.value;
						}
					}
					return combobox.selectedItem.data;
				}
				else if(_propertyVo.propertyType == PropertyType.NUMBER || _propertyVo.propertyType == PropertyType.COLOR)
				{
					return parseInt(valueLabel.text);			
				}else if(_propertyVo.propertyType == PropertyType.STRING)
				{
					return valueLabel.text;
				}
				return 0;
			}
			
			protected function ridio_itemClickHandler(event:MouseEvent):void
			{
				update();				
			}
			
		]]>
	</fx:Script>
		
	<s:Label id="nameLabel" x="10" y="10" width="60%" text="标签" toolTip="{toolTips}"/>
	<s:TextInput id="valueLabel" x="84" y="3" width="218" borderColor="#FFFFFF" chromeColor="#FFFFFF"
				 tabEnabled="true"
				 contentBackgroundColor="#F2F2F2" focusOut="valueLabel_focusOutHandler(event)" keyDown="valueLabel_keyDownHandler(event)"
				 prompt="value input"/>
	<mx:ComboBox id="combobox" x="84" y="3" width="111" change="combobox_changeHandler(event)"/>
	<mx:ColorPicker id="colorPicker" x="243" y="3"/>
	<fx:Declarations>
		<s:RadioButtonGroup id="radio"/>
	</fx:Declarations>
	<s:Group id="radioGroup">
		<s:RadioButton id="radio1" x="110" y="5" label="true" groupName="radio" click="ridio_itemClickHandler(event);"/>
		<s:RadioButton id="radio2" x="172" y="5" label="false" groupName="radio" click="ridio_itemClickHandler(event);"/>
	</s:Group>
</s:Group>
